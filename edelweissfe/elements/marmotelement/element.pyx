#!/usr/bin/env python3
# -*- coding: utf-8 -*-
#  ---------------------------------------------------------------------
#
#  _____    _      _              _         _____ _____
# | ____|__| | ___| |_      _____(_)___ ___|  ___| ____|
# |  _| / _` |/ _ \ \ \ /\ / / _ \ / __/ __| |_  |  _|
# | |__| (_| |  __/ |\ V  V /  __/ \__ \__ \  _| | |___
# |_____\__,_|\___|_| \_/\_/ \___|_|___/___/_|   |_____|
#
#
#  Unit of Strength of Materials and Structural Analysis
#  University of Innsbruck,
#  2017 - today
#
#  Matthias Neuner matthias.neuner@uibk.ac.at
#
#  This file is part of EdelweissFE.
#
#  This library is free software; you can redistribute it and/or
#  modify it under the terms of the GNU Lesser General Public
#  License as published by the Free Software Foundation; either
#  version 2.1 of the License, or (at your option) any later version.
#
#  The full text of the license can be found in the file LICENSE.md at
#  the top level directory of EdelweissFE.
#  ---------------------------------------------------------------------
# Created on Thu Apr 27 08:35:06 2017

# @author: matthias

import numpy as np

cimport cython
cimport libcpp.cast
cimport numpy as np

from edelweissfe.utils.exceptions import CutbackRequest

# Point Marmot's warning channel at stdout, once, when this extension is imported. MarmotJournal
# writes into a null streambuf unless a consumer calls setMSGOutputDirection, and nothing here ever
# did -- so every warning Marmot raised on this path was discarded before it could be printed.
# std::cout is where the solver's own output goes, so a redirected run log captures it.
MarmotJournal.setMSGOutputDirection(cout)


cimport edelweissfe.elements.marmotelement.element

mapLoadTypes={
        "pressure" : DistributedLoadTypes.Pressure,
        "surface torsion" : DistributedLoadTypes.SurfaceTorsion,
        "surface traction" : DistributedLoadTypes.SurfaceTraction
     }

mapStateTypes={
        "geostatic stress" : StateTypes.GeostaticStress,
        "sdvini" : StateTypes.MarmotMaterialStateVars,
        "initialize material": StateTypes.MarmotMaterialInitialization
     }


#: Number of nodes of the Ensight shapes, see MarmotElementWrapper.visualizationNodes.
_ENSIGHT_SHAPE_NODE_COUNTS = {"point": 1, "bar2": 2, "bar3": 3, "tria3": 3, "tria6": 6, "quad4": 4, "quad8": 8,
                              "tetra4": 4, "tetra10": 10, "hexa8": 8, "hexa20": 20}


@cython.final  # no subclassing -> cpdef with nogil possible
cdef class MarmotElementWrapper:
    # cdef classes cannot subclass. Hence we do not subclass from the BaseElement,
    # but still we follow the interface for compatiblity.

    def __init__(self, elementType, elNumber):
        """This element serves as a wrapper for MarmotElements.

        For the documentation of MarmotElements, please refer to `Marmot
        <https://github.com/MAteRialMOdelingToolbox/Marmot/>`_.

        Parameters
        ----------
        elementType
            The Marmot element which should be represented, e.g., CPE4.
        elNumber
            The number of the element."""

        self._elNumber = elNumber
        self._elType = elementType

        self._nNodes = self.marmotElement.getNNodes()
        self._nSpatialDimensions = self.marmotElement.getNSpatialDimensions()
        self._nDof = self.marmotElement.getNDofPerElement()

        cdef vector[vector[string]] fields = self.marmotElement.getNodeFields()
        self._fields = [[s.decode("utf-8") for s in n] for n in fields]

        cdef vector[int] permutationPattern = self.marmotElement.getDofIndicesPermutationPattern()
        self._dofIndicesPermutation = np.asarray(permutationPattern)

        self._ensightType = self.marmotElement.getElementShape().decode("utf-8")

        self._hasMaterial = False

    def __cinit__(self, elementType, elNumber):
        """This C-level method is responsible for actually creating the MarmotElement.

        Parameters
        ----------
        elementType
            The Marmot element which should be represented, e.g., CPE4.
        elNumber
            The number of the element."""

        try:
            self.marmotElement = MarmotElementFactory.createElement(elementType.upper().encode("utf-8"), self._elNumber)
        except ValueError:
            raise NotImplementedError("Marmot element {:} not found in library.".format(elementType))

    @property
    def elNumber(self):
        return self._elNumber

    @property
    def hasKernels(self):
        """True: a Marmot element carries a material and a state; see BaseElement.hasKernels."""
        return True

    @property
    def nSpatialDimensions(self):
        return self._nSpatialDimensions

    @property
    def elType(self):
        return self._elType

    @property
    def nodes(self):
        return self._nodes

    @property
    def nNodes(self):
        return self._nNodes

    @property
    def nDof(self):
        return self._nDof

    @property
    def fields(self):
        return self._fields

    @property
    def dofIndicesPermutation(self):
        return self._dofIndicesPermutation

    @property
    def ensightType(self):
        return self._ensightType

    @property
    def visualizationNodes(self):
        # elements coupling several geometries (e.g., a bond element: the nodes of a bar, followed by the nodes of
        # its host element) are visualized by the leading nodes of their Ensight shape only
        nShapeNodes = _ENSIGHT_SHAPE_NODE_COUNTS.get(self._ensightType, self._nNodes)
        return self._nodes[:nShapeNodes] if nShapeNodes < self._nNodes else self._nodes

    @property
    def hasMaterial(self):
        return self._hasMaterial

    def setNodes (self, nodes):
        """Assign the nodes coordinates to the underyling MarmotElement"""

        self._nodes = nodes
        self.nodeCoordinates = np.concatenate([node.coordinates for node in nodes])
        self.marmotElement.assignNodeCoordinates(&self.nodeCoordinates[0])

    def setProperties(self, elementProperties):
        """Assign a set of properties to the underyling MarmotElement"""

        self._elementProperties = elementProperties

        self.marmotElement.assignProperty(
                ElementProperties(
                        &self._elementProperties[0],
                        self._elementProperties.shape[0]))

    def assignProperty(self, str propertyName, properties):
        """Assign a single property of the element by name."""

        cdef double[::1] _properties = np.ascontiguousarray(
                np.atleast_1d(np.asarray(properties, dtype=np.float64)))

        # The count travels with the pointer: the values come from a user-written input file and
        # the element reads a fixed number per property name, so without it a short list is not an
        # error but an out-of-bounds read landing in a material coefficient.
        self.marmotElement.assignProperty(
                propertyName.encode("UTF-8"),
                &_properties[0],
                _properties.shape[0])

    def getPropertyNames(self):
        """Get the names of all the valid properties of the element."""

        cdef vector[string] names = self.marmotElement.getPropertyNames()
        return [name.decode("utf-8") for name in names]

    @property
    def propertyNames(self):
        """Get the names of all the valid properties of the element."""
        return self.getPropertyNames()

    def initializeElement(self, ):
        """Let the underlying MarmotElement initialize itself"""
        self.marmotElement.initializeYourself()

    def setMaterial(self, materialName, materialProperties):
        """Assign a material and material properties to the underlying MarmotElement.
        Furthermore, create two sets of state vars:

            - the actual set,
            - and a temporary set for backup in nonlinear iteration schemes.
        """
        self._materialProperties = materialProperties
        try:
            self.marmotElement.assignProperty(
                    MarmotMaterialSection(
                            materialName.upper().encode("UTF-8"),
                            &self._materialProperties[0],
                            self._materialProperties.shape[0]))
        except ValueError:
            raise NotImplementedError("Marmot material {:} not found in library.".format(materialName.upper()))

        self.nStateVars = self.marmotElement.getNumberOfRequiredStateVars()

        self._stateVars = np.zeros(self.nStateVars)
        self._stateVarsTemp = np.zeros(self.nStateVars)

        self.marmotElement.assignStateVars(&self._stateVarsTemp[0], self.nStateVars)

        self._hasMaterial = True

    cpdef void _initializeStateVarsTemp(self, ) noexcept nogil:
        self._stateVarsTemp[:] = self._stateVars

    def setInitialCondition(self,
                            stateType,
                            const double[::1] values):
        """Assign initial conditions to the underlying Marmot element"""

        if not self._hasMaterial:
            raise Exception("Element {:} has no material assigned!".format(self._elNumber))

        self._initializeStateVarsTemp()
        self.marmotElement.setInitialConditions(mapStateTypes[stateType], &values[0])
        self.acceptLastState()

    cpdef void computeKernels(self,
                              double[::1] Ke,
                              double[::1] Pe,
                              const double[::1] U,
                              const double[::1] dU,
                              double time,
                              double dTime) except *:
        """Evaluate residual and stiffness for given time, field, and field increment."""

        if not self._hasMaterial:
            raise Exception("Element {:} has no material assigned!".format(self._elNumber))

        try:
            with nogil:
                self._initializeStateVarsTemp()

                self.marmotElement.computeKernels(&U[0],
                                                  &dU[0],
                                                  &Pe[0],
                                                  &Ke[0],
                                                  time,
                                                  dTime)
        except (RuntimeError, ValueError) as e:
            raise CutbackRequest(str(e), 0.5)

    cpdef void computeKernelsExplicit(self,
                                      double[::1] Pe,
                                      const double[::1] U,
                                      const double[::1] dU,
                                      double time,
                                      double dTime) except *:
        """Evaluate residual and stiffness for given time, field, and field increment."""

        if not self._hasMaterial:
            raise Exception("Element {:} has no material assigned!".format(self._elNumber))

        try:
            with nogil:
                self._initializeStateVarsTemp()

                self.marmotElement.computeKernelsExplicit(&U[0],
                                                          &dU[0],
                                                          &Pe[0],
                                                          time,
                                                          dTime)
        except (RuntimeError, ValueError) as e:
            raise CutbackRequest(str(e), 0.5)

    def computeDistributedLoad(self,
                               str loadType,
                               double[::1] P,
                               double[::1] K,
                               int faceID,
                               const double[::1] load,
                               const double[::1] U,
                               double time,
                               double dTime):
        """Evaluate residual and stiffness for given time, field, and field increment due to a surface load."""

        self.marmotElement.computeDistributedLoad(mapLoadTypes[loadType],
                                                  &P[0],
                                                  &K[0],
                                                  faceID,
                                                  &load[0],
                                                  &U[0],
                                                  time,
                                                  dTime)

    def computeBodyForce(self,
                         double[::1] P,
                         double[::1] K,
                         const double[::1] load,
                         const double[::1] U,
                         double time,
                         double dTime):
        """Evaluate residual and stiffness for given time, field, and field increment due to a volume load."""

        self.marmotElement.computeBodyForce(
                                    &P[0],
                                    &K[0],
                                    &load[0],
                                    &U[0],
                                    time,
                                    dTime)

    def computeLumpedInertia(self, double[::1] M):
        """Compute the lumped mass matrix of the underlying MarmotElement"""

        self.marmotElement.computeLumpedInertia(&M[0])

    def computeLumpedDamping(self, double[::1] C):
        """Compute the lumped damping of the underlying MarmotElement"""

        self.marmotElement.computeLumpedDamping(&C[0])

    def computeConsistentInertia(self, double[::1] M):
        """Compute the consistent (full) mass matrix of the underlying MarmotElement.

        Written into ``M`` in the element's flat ``nDof * nDof`` layout -- the same layout
        ``computeKernels`` writes the stiffness into, so an entity slice of a VIJ system matrix can
        be handed over directly and the assembled mass shares the stiffness' sparsity pattern."""

        self.marmotElement.computeConsistentInertia(&M[0])

    def computeCriticalTimeStepForExplicitDynamics(self, double[::1] Q):
        """Compute the critical time step for explicit dynamics of the underlying MarmotElement"""
        cdef double criticalTimeStep = 1e36
        self.marmotElement.computeCriticalTimeStepForExplicitDynamics(criticalTimeStep, &Q[0])
        return criticalTimeStep

    def computeInternalEnergy(self):
        """Compute the internal energy of the underlying MarmotElement"""
        cdef double internalEnergy = 0.0
        self.marmotElement.computeInternalEnergy(internalEnergy)
        return internalEnergy

    def acceptLastState(self, ):
        """Accept the computed state (in nonlinear iteration schemes)."""

        self._stateVars[:] = self._stateVarsTemp

    def getStateVars(self):
        """Return a copy of the converged quadrature-point state-variable buffer."""

        return np.asarray(self._stateVars).copy()

    def setStateVars(self, double[::1] values):
        """Overwrite the converged and trial state-variable buffers in place (so the MarmotElement's
        assigned pointer to the trial buffer stays valid). Used by adaptive refinement to transfer
        history to child elements."""

        if values.shape[0] != self._stateVars.shape[0]:
            raise ValueError(
                "setStateVars: expected {:} state variables, got {:}".format(
                    self._stateVars.shape[0], values.shape[0]))
        self._stateVars[:] = values
        self._stateVarsTemp[:] = values

    def resetToLastValidState(self, ):
        """Reset to the last valid state."""

    def getResultArray(self, result, quadraturePoint, getPersistentView=True):
        """Get the array of a result, possibly as a persistent view which is continiously
        updated by the underlying MarmotElement."""

        if not self._hasMaterial:
            raise Exception("Element {:} has no material assigned!".format(self._elNumber))

        cdef string result_ = result.encode("UTF-8")
        return np.array(self.getStateView(result_, quadraturePoint), copy= not getPersistentView)

    cdef double[::1] getStateView(self, string result, int quadraturePoint, ):
        """Directly access the state vars of the underlying MarmotElement"""

        if not self._hasMaterial:
            raise Exception("Element {:} has no material assigned!".format(self._elNumber))

        cdef StateView res = self.marmotElement.getStateView(result, quadraturePoint)

        return <double[:res.stateSize]> (res.stateLocation)

    def getStateVarSlice(self, name):
        """Locate a named state variable within one per-quadrature-point state block.

        Returns the (offset, size) of the named variable relative to the start of a per-quadrature-
        point block, computed from the pointer the MarmotElement hands out for quadrature point 0.
        Used by adaptive refinement to route different state variables to different transfer
        strategies (copy / project / reset)."""

        if not self._hasMaterial:
            raise Exception("Element {:} has no material assigned!".format(self._elNumber))

        cdef string name_ = name.encode("UTF-8")
        cdef StateView res = self.marmotElement.getStateView(name_, 0)
        cdef Py_ssize_t offset = res.stateLocation - &self._stateVarsTemp[0]
        return int(offset), int(res.stateSize)

    def getCoordinatesAtCenter(self):
        """Compute the underlying MarmotElement centroid coordinates."""

        return np.asarray (self.marmotElement.getCoordinatesAtCenter())

    def getCoordinatesAtQuadraturePoints(self):
        """Compute the underlying MarmotElement qp coordinates."""

        return np.asarray (self.marmotElement.getCoordinatesAtQuadraturePoints())

    def getNumberOfQuadraturePoints(self):
        """Compute the underlying MarmotElement qp coordinates."""

        return self.marmotElement.getNumberOfQuadraturePoints()

    def __dealloc__(self):
        del self.marmotElement

    def getVIJContributionSize(self) -> int:
        """Return the number of entries this entity contributes to the VIJ (COO) system matrix."""
        return self.nDof**2

    def shapeVIJContribution(self, flat_view: np.ndarray) -> np.ndarray:
        """Keep the view flat for Cython 1-D memoryview compatibility."""
        return flat_view

    def initializeVIJContribution(self, idcs: np.ndarray, I_: np.ndarray, J_: np.ndarray, offset: int) -> None:

        """Initialize the I and J arrays for the VIJ (COO) system matrix assembly. """

        n = len(idcs)
        VIJLocations = np.tile(idcs, (n, 1))
        I_[offset : offset + n**2] = VIJLocations.flatten()
        J_[offset : offset + n**2] = VIJLocations.flatten("F")
